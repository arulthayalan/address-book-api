
buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.7.RELEASE")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    }
}

plugins {
    id 'net.ltgt.apt' version '0.8'
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: 'idea'

group 'au.com.pwc.api'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

 springBoot {
     mainClassName = 'au.com.pwc.addressbook.Application'
 }

sourceSets {
    functionalTest {
        java.srcDir file('src/functionalTest/groovy')
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation 'org.mapstruct:mapstruct-jdk8:1.3.0.Final'
    apt 'org.mapstruct:mapstruct-processor:1.3.0.Final'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    runtime 'com.h2database:h2:1.4.199'

    testImplementation 'org.codehaus.groovy:groovy-all:2.4.1'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testImplementation  'org.spockframework:spock-spring:1.1-groovy-2.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'io.rest-assured:rest-assured:4.0.0'
    testImplementation 'io.rest-assured:json-path:4.0.0'
    testImplementation 'io.rest-assured:xml-path:4.0.0'
}


task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output
    classpath = sourceSets.functionalTest.runtimeClasspath
    binResultsDir = file("$testResultsDir/functionalTest")
}
